%%[
	IF _messagecontext == 'SEND' THEN
		/* Define the field to use to associate the send with a CRM object */
		SET @WhatId = AttributeValue('%%WhatId%%')

		/* Check to see if we've already created a task for this send */
		SET @rs = LookupOrderedRows("EmailLogCRM",1,"JobID Desc","JobID",jobid,"ListID",listid,"BatchID",_JobSubscriberBatchID,"SubscriberID",subscriberid)

		IF RowCount(@rs) == 0 THEN
			/* Log the record - Helps to ensure only on update to CRM takes place */
			SET @logged = UpsertDE('EmailLogCRM',4,'JobID',jobid,'ListID',listid,'BatchID',_JobSubscriberBatchID,'SubscriberID',subscriberid,'SubscriberKey',_subscriberkey,'EmailAddress',emailaddr,'EmailName',emailname_,'WhatId',@WhatId,'VAWP',view_email_url,'HTMLContent',@HTMLContent)

			IF @logged == 1 THEN
				/* Scrape VAWP to get HTML for DE Log */
				SET @HTMLContent = HttpGet(view_email_url)
				SET @HTMLContent = Replace(Replace(Replace(Replace(Replace(Replace(@HTMLContent, '"', "'"),char(13),""),char(10),""),char(09),""), char(11),""),char(12),"")
				SET @HTMLContent = Replace(@HTMLContent, "'http://www.w3.org/TR/html4/loose.dtd'","")

				/* Update Salesforce CRM */
				SET @SFEmailID = CreateSalesforceObject("EmailMessage",8,"HTMLBODY",@HTMLContent, "SUBJECT",emailname_,"FROMNAME",replyname,"FROMADDRESS",replyemailaddress,"TOADDRESS",emailaddr,"INCOMING",0,"STATUS",3,"RELATEDTOID",@WhatId)
				SET @RelationID = CreateSalesforceObject("EmailMessageRelation",4,"EmailMessageId",@SFEmailID,"RelationAddress",emailaddr,"RelationId",_subscriberkey,"RelationType","ToAddress")
			ENDIF
		ENDIF
	ENDIF
]%%
