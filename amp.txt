%%[
	IF _messagecontext == 'SEND' OR _messagecontext == 'PREVIEW' THEN
		/* Define the field to use to associate the send with a CRM object */
		SET @WhatId = AttributeValue('%%WhatId%%')

		/* Check to see if we've already created a task for this send */
		SET @rs = LookupOrderedRows("EmailTaskLogTESTER",1,"JobID Desc","JobID",jobid,"ListID",listid,"BatchID",_JobSubscriberBatchID,"SubscriberID",subscriberid)

		IF RowCount(@rs) == 0 THEN
			/* Scrape VAWP to get HTML for DE Log */

			SET @HTMLContent = HttpGet(view_email_url)
			SET @HTMLContent = Replace(@HTMLContent, '"', "'")
			SET @HTMLContent = Replace(Replace(@HTMLContent ,char(13),""), char(10),"")
			SET @HTMLContent = Replace(@HTMLContent, " ", "")
			SET @HTMLContent = Replace(@HTMLContent, "DOCTYPEhtmlPUBLIC", "DOCTYPE HTML PUBLIC")

			/* Update Salesforce CRM */

			SET @SFEmailID = CreateSalesforceObject("EmailMessage",8,"HTMLBODY",@HTMLContent, "SUBJECT",emailname_,"FROMNAME","Marketing Cloud","FROMADDRESS","communication@communication.industry.nsw.gov.au","TOADDRESS",emailaddr,"INCOMING",0,"STATUS",3,"RELATEDTOID",@WhatId)
			SET @RelationID = CreateSalesforceObject("EmailMessageRelation",4,"EmailMessageId",@SFEmailID,"RelationAddress",emailaddr,"RelationId",_subscriberkey,"RelationType","ToAddress")

			UpsertDE('EmailTaskLogTESTER',4,'JobID',jobid,'ListID',listid,'BatchID',_JobSubscriberBatchID,'SubscriberID',subscriberid,'SubscriberKey',_subscriberkey,'EmailAddress',emailaddr,'EmailName',emailname_,'WhatId',@WhatId,'VAWP',view_email_url,'HTMLContent',@HTMLContent)
		ENDIF
	ENDIF
]%%